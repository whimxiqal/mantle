plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

def determinePatchVersion() {
    // get the name of the last tag
    def tagInfo = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = tagInfo
    }
    tagInfo = tagInfo.toString()
    if (!tagInfo.contains('-')) {
        return 0
    }
    return tagInfo.split("-")[1]
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'

    project.ext.publishApi = false
    project.ext.majorVersion = '0'
    project.ext.minorVersion = '0'
    project.ext.patchVersion = '0' //determinePatchVersion()
    project.ext.apiVersion = project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.fullVersion = project.ext.apiVersion + '.' + project.ext.patchVersion
    version = project.ext.fullVersion

    jar.enabled = false

    sourceCompatibility = JavaVersion.toVersion(8)
    targetCompatibility = JavaVersion.toVersion(8)

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    license {
        header = rootProject.file('LICENSE.txt')
        include 'src/**/*.java'
        newLine = true
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

}
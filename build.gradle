plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

defaultTasks 'licenseFormat', 'build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.cadixdev.licenser'

    project.ext.publish = false
    project.ext.snapshot = true
    project.ext.majorVersion = '0'
    project.ext.minorVersion = '1'
    project.ext.patchVersion = '1'
    project.ext.fullVersion = project.ext.majorVersion + '.'+ project.ext.minorVersion + '.' + project.ext.patchVersion + (project.ext.snapshot ? "-SNAPSHOT" : "")
    version = project.ext.fullVersion
    group = 'net.whimxiqal.mantle'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    license {
        header = rootProject.file('LICENSE.txt')
        include 'src/**/*.java'
        newLine = true
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.charSet = 'UTF-8'
        options.links(
                'https://javadoc.io/doc/org.jetbrains/annotations/23.0.0'
        )
        options.addStringOption('Xdoclint:none', '-quiet')
    }

}
plugins {
    id 'mantle.java-conventions'
//    id 'antlr'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

dependencies {

    // https://mvnrepository.com/artifact/org.antlr/antlr
    implementation 'org.antlr:antlr4:4.9.3'

    // https://mvnrepository.com/artifact/net.kyori/adventure-api
    api 'net.kyori:adventure-api:4.11.0'
    api 'net.kyori:adventure-text-serializer-plain:4.11.0'

    // https://mvnrepository.com/artifact/com.vmware.antlr4-c3/antlr4-c3
    // Internally used for command completion
    implementation 'com.vmware.antlr4-c3:antlr4-c3:1.1'
}

test {
    useJUnitPlatform()
}

if (project.hasProperty('ossrhToken') && project.hasProperty('ossrhTokenPassword') && project.ext.publish) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                name = "sonatype"
                url = project.ext.snapshot
                        ? "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                        : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username findProperty("ossrhToken") as String
                    password findProperty("ossrhTokenPassword") as String
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId = 'net.whimxiqal.mantle'
                artifactId = 'common'
                version = project.ext.fullVersion
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'Mantle Common'
                    description = 'A Minecraft Command Framwork on top of ANTLR4'
                    url = 'https://github.com/whimxiqal/mantle'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'whimxiqal'
                            name = 'whimxiqal'
                            url = 'https://whimxiqal.net'
                            email = 'whimxiqal@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/whimxiqal/mantle.git'
                        developerConnection = 'scm:git:ssh://github.com:whimxiqal/mantle.git'
                        url = 'https://github.com/whimxiqal/mantle'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/whimxiqal/mantle/issues'
                    }
                }
            }
        }

        signing {
            useGpgCmd()
            sign configurations.archives
            sign publishing.publications.mavenJava
            required = true
        }
    }
}
plugins {
    id 'java'
    id 'java-library'
    id 'mantle.java-conventions'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

repositories {
    maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation project(':common')
    implementation 'io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT'

    testImplementation project(':crust')
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.93.2'
}

if (project.hasProperty('ossrhToken') && project.hasProperty('ossrhTokenPassword') && project.ext.publish) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                name = "sonatype"
                url = project.ext.snapshot
                        ? "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                        : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username findProperty("ossrhToken") as String
                    password findProperty("ossrhTokenPassword") as String
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId = 'net.whimxiqal.mantle'
                artifactId = 'paper'
                version = project.ext.fullVersion
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'Mantle Paper'
                    description = 'A Minecraft Command Framework using ANTLR'
                    url = 'https://github.com/whimxiqal/mantle'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'whimxiqal'
                            name = 'whimxiqal'
                            url = 'https://whimxiqal.net'
                            email = 'whimxiqal@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/whimxiqal/mantle.git'
                        developerConnection = 'scm:git:ssh://github.com:whimxiqal/mantle.git'
                        url = 'https://github.com/whimxiqal/mantle'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/whimxiqal/mantle/issues'
                    }
                }
            }
        }

        signing {
            useGpgCmd()
            sign configurations.archives
            sign publishing.publications.mavenJava
            required = true
        }
    }
}